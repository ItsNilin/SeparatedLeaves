plugins {
    id "com.github.johnrengelman.shadow"
    id "com.modrinth.minotaur" version "2.+"
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

//loom {
//    accessWidenerPath = project(":common").loom.accessWidenerPath
//
//    forge {
//        convertAccessWideners = true
//        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
//
//        mixinConfig "separatedleaves-common.mixins.json"
//        mixinConfig "separatedleaves.mixins.json"
//    }
//}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "separated-leaves"
    versionNumber = "${rootProject.mod_version}-forge-${rootProject.latest_version}"
    versionName = "${rootProject.mod_version} Forge ${rootProject.latest_version}"
    versionType = "release"
    uploadFile = remapJar
    gameVersions = ["1.19.3", "1.19.4", "1.20", "1.20.1", "1.20.2", "1.20.3", "1.20.4"]
    loaders = ["forge"]
    dependencies {
        required.project "architectury-api"
        optional.project "biomesyougo"
        optional.project "ecologics"
        optional.project "hex-casting"
        optional.project "premium-wood"
        optional.project "quark"
        optional.project "regions-unexplored"
    }
    changelog = rootProject.file("CHANGELOG_LATEST.md").text

    syncBodyFrom = rootProject.file("README.md").text
    tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
}

curseforge {
    project {
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        id = "905482"
        changelog = rootProject.file("CHANGELOG_LATEST.md").text
        changelogType = "markdown"
        releaseType = "release"
        addGameVersion "1.19.3"
        addGameVersion "1.19.4"
        addGameVersion "1.20"
        addGameVersion "1.20.1"
        addGameVersion "1.20.2"
        addGameVersion "1.20.3"
        addGameVersion "1.20.4"
        addGameVersion "Forge"

        relations {
            requiredDependency "architectury-api"
            optionalDependency "atmospheric"
            optionalDependency "autumnity"
            optionalDependency "biomes-o-plenty"
            optionalDependency "ecologics"
            optionalDependency "environmental"
            optionalDependency "hexcasting"
            optionalDependency "malum"
            optionalDependency "oh-the-biomes-youll-go"
            optionalDependency "premium-wood"
            optionalDependency "quark"
            optionalDependency "regions-unexplored"
            optionalDependency "upgrade-aquatic"
            optionalDependency "windsweptmod"
        }
        mainArtifact(remapJar) {
            displayName = "${rootProject.mod_version} Forge ${rootProject.latest_version}"
        }
        options {
            javaVersionAutoDetect = false
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/neoforge.mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
    archiveClassifier = "neoforge-${rootProject.latest_version}"
}
