plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.modrinth.minotaur" version "2.+"
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "separatedleaves"
    versionNumber = "${rootProject.mod_version}-fabric-${rootProject.latest_version}"
    versionName = "${rootProject.mod_version} Fabric ${rootProject.latest_version}"
    versionType = "release"
    uploadFile = remapJar
    gameVersions = ["1.19.3", "1.19.4", "1.20", "1.20.1", "1.20.2", "1.20.3", "1.20.4"]
    loaders = ["fabric"]
    dependencies {
        required.project "architectury-api"
        required.project "fabric-api"
        optional.project "biomesyougo"
        optional.project "ecologics"
        optional.project "hex-casting"
        optional.project "premium-wood"
        optional.project "regions-unexplored"
    }
    changelog = rootProject.file("CHANGELOG_LATEST.md").text

    syncBodyFrom = rootProject.file("README.md").text
    tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
}

curseforge {
    project {
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        id = "905482"
        changelog = rootProject.file("CHANGELOG_LATEST.md").text
        changelogType = "markdown"
        releaseType = "release"
        addGameVersion "1.19.3"
        addGameVersion "1.19.4"
        addGameVersion "1.20"
        addGameVersion "1.20.1"
        addGameVersion "1.20.2"
        addGameVersion "1.20.3"
        addGameVersion "1.20.4"
        addGameVersion "Fabric"

        relations {
            requiredDependency "architectury-api"
            requiredDependency "fabric-api"
            optionalDependency "ecologics"
            optionalDependency "hexcasting"
            optionalDependency "oh-the-biomes-youll-go-fabric"
            optionalDependency "premium-wood"
            optionalDependency "regions-unexplored"
        }
        mainArtifact(remapJar) {
            displayName = "${rootProject.mod_version} Fabric ${rootProject.latest_version}"
        }
        options {
            javaVersionAutoDetect = false
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "fabric-${rootProject.latest_version}"
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
